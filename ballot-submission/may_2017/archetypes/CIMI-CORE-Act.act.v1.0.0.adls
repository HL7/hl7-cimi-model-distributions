archetype (adl_version=2.0.6; rm_release=0.0.1)
	CIMI-CORE-Act.act.v1.0.0

specialize
  CIMI-CORE-StatementTopic.statement_topic.v1

language
  original_language = <[ISO_639-1::en]>

description
  original_author = <
    ["name"] = <"Claude Nanjo">
    ["organisation"] = <"Cognitive Medical Systems">
    ["email"] = <"cnanjo@cognitivemedicine.com">
    ["date"] = <"11/24/2016">
  >
  details = <
    ["en"] = <
      language = <[ISO_639-1::en]>
      purpose = <"Top-level reference archetype for CIMI 'clinical statement' pattern">
      use = <"Use as a specialisation precursor for other high-level reference archetypes.">
      keywords = <"Act","topic">
    >
  >
  other_details = <["model_level"] = <"reference">>
  lifecycle_state = <"unmanaged">
  other_contributors = <"Linda Bird <lbi@ihtsdo.org>","Joey Coyle <joey@xcoyle.com>","Stan Huff <Stan.Huff@imail.org>","Harold Solbrig <Solbrig.Harold@mayo.edu>","Thomas Beale <thomas.beale@oceaninformatics.com>">
  custodian_namespace = <"org.opencimi">
  custodian_organisation = <"Clinical Information Modeling Initiative <http://opencimi.org>">
  licence = <"Creative Commons CC-BY <https://creativecommons.org/licenses/by/3.0/>">
  copyright = <"Copyright (c) Clinical Information Modelling Initiative <http://opencimi.org>">
  ip_acknowledgements = <
    ["loinc"] = <"This content from LOINC® is copyright © 1995 Regenstrief Institute, Inc. and the LOINC Committee, and available at no cost under the license at http://loinc.org/terms-of-use">
    ["snomed"] = <"This specification includes SNOMED Clinical Terms® (SNOMED CT®) which is used by permission of the International Health Terminology Standards Development Organization (IHTSDO). All rights reserved. SNOMED CT® was originally created by the College of American Pathologists. 'SNOMED' and 'SNOMED CT' are registered trademarks of the IHTSDO (www.ihtsdo.org). Use of any SNOMED CT content (including codes, identifiers, terms, expressions or derivatives) in any operational system (including any Data Creation System or Data Analysis System as defined in the SNOMED CT Affiliate License Agreement) is subject to the terms and conditions of the SNOMED CT Affiliate License (www.ihtsdo.org/license.pdf) or to a written variation to that agreement that has been agreed and formally executed between the IHTSDO and the Affiliate Licensee.">
  >

definition
	Act[id1.1] matches {
    /key[id2.1] matches {[ac0.1]}
    /code[id3.1] matches {[ac0.2]}
    description matches {
      PLAIN_TEXT[id0.2]
    }
    priority matches {
      CODED_TEXT[id0.3]
    }
    reason matches {
      TEXT[id0.4]
    }
    status matches {
      CODED_TEXT[id0.5]
    }
    method matches {
      CODED_TEXT[id0.6]
    }
  }

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Act">
				description = <"Description of a healthcare action, independent of action context.">
			>
      ["id2.1"] = <
        text = <"Key">
      >
      ["id3.1"] = <
        text = <"Code">
      >
      ["id0.1"] = <
        text = <"Type">
        description = <"Concept representing the type of act.">
      >
      ["id0.2"] = <
        text = <"Description">
        description = <"An account of the act in narrative form.">
      >
      ["id0.3"] = <
        text = <"Priority">
        description = <"Priority assigned to a procedure.">
      >
      ["id0.4"] = <
        text = <"Reason">
        description = <"The reason for performing this act.">
      >
      ["id0.5"] = <
        text = <"Status">
        description = <"Statuses associated with this act.">
      >
      ["id0.6"] = <
        text = <"Method">
        description = <"Technique or way of accomplishing the act. E.g., endoscopic, arthroscopic, etc...">
      >
      ["ac0.1"] = <
        text = <"Act statement topic key range value set">
        description = <"Value set containing concept representing an Act Statement Topic">
      >
      ["ac0.2"] = <
        text = <"Act statement topic code range value set">
        description = <"Range for the associated procedure SWEC attribute">
      >
		>
	>

  term_bindings = <
    ["snomed-ct"] = <
      items = <
        ["id2.1"] = <http://example.com/valuesets/act_statement_topic_key_vs.v0.0.1>
        ["id3.1"] = <http://example.com/valuesets/act_statement_topic_code_vs.v0.0.1>
        ["ac0.1"] = <http://example.com/valuesets/act_statement_topic_key_range_vs.v0.0.1>
        ["ac0.2"] = <http://example.com/valuesets/act_statement_topic_code_range_vs.v0.0.1>
        ["id0.3"] = <http://example.com/valuesets/act_statement_topic_priority_vs.v0.0.1> -- |Priority (attribute)|
        ["id0.6"] = <http://example.com/valuesets/act_statement_topic_method_vs.v0.0.1> -- |Method (attribute)|
      >
    >
    ["loinc"] = <
      items = <
        ["id0.1"] = <http://loinc.org/id/29300-1> -- | Procedure Type|
      >
    >
  >
